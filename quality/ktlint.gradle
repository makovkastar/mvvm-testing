repositories {
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:${versions.ktlint}"
}

def outputDir = "${buildDir}/reports/ktlint/"
def outputFile = "${outputDir}ktlint.xml"
def inputFiles = project.fileTree(dir: "src", include: "**/*.kt")

task ktlint(type: JavaExec, group: "verification") {
    inputs.files(inputFiles)
    outputs.dir(outputDir)
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args = [
        "src/**/*.kt",
        "--reporter=plain",
        "--reporter=checkstyle,output=${outputFile}"
    ]
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
